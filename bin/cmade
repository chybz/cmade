#!/usr/bin/env -S cmake -P
# -*- CMake -*-

set(CMADE "cmade")
set(CMDEPS "CMakeDeps.txt")
set(CMADE_VERSION "0.1.0")
set(CMADE_CACHE ".cmade/cache")
set(CMADE_ENV ".cmade/env")
list(
    APPEND
    CMADE_OPTIONS
    "verbose"
    "simulate"
    "no-cache"
)

##############################################################################
#
# Utility functions
#
##############################################################################
function(show_help)
    message("\
CMake Dependency Installer v${CMADE_VERSION}

usage: ${CMADE} [OPTIONS] COMMAND

Options:
  --verbose     Verbose operation
  --simulate    Echo commands instead of running them
  --no-cache    Ignore/erase existing repositories

Commands:
  install       Install dependencies from ${CMDEPS}
  help          Display this information
")
endfunction()

function(die MSG)
    message(FATAL_ERROR "CMade: error: ${MSG}")
endfunction()

function(msg MSG)
    message("CMade: ${MSG}")
endfunction()

function(info MSG)
    if(CMADE_VERBOSE)
        msg(${MSG})
    endif()
endfunction()

function(setg VAR VAL)
    set(${VAR} "${VAL}" CACHE INTERNAL "${VAR}")
endfunction()

function(run_prog)
    cmake_parse_arguments(RUN "" "DIR" "CMD" ${ARGN})

    if(CMADE_SIMULATE)
        list(PREPEND RUN_CMD "echo")
    endif()

    execute_process(
        COMMAND ${RUN_CMD}
        WORKING_DIRECTORY "${RUN_DIR}"
        RESULTS_VARIABLE RC
    )

    if(RC)
        list(JOIN ARGV " " RUN_CMD)
        die("command failed: ${RUN_CMD}")
    endif()
endfunction()

function(download URL FILE)
    if(CMADE_SIMULATE)
        msg("download ${URL} to ${FILE}")
    else()
        file(DOWNLOAD ${URL} ${FILE} STATUS ST)
    endif()

    list(GET ST 0 RC)

    if(RC)
        die("download of ${URL} failed: ${ST}")
    endif()
endfunction()

function(unique_dir PATH VAR)
    file(GLOB PATHS "${PATH}/*")

    foreach(PATH ${PATHS})
        if(IS_DIRECTORY ${PATH})
            list(APPEND ALL_DIRS ${PATH})
        endif()
    endforeach()

    list(LENGTH ALL_DIRS DIRS)

    if(DIRS EQUAL 0)
        die("no directories found in ${PATH}")
    elseif(DIRS GREATER 1)
        die("multiple directories found ${PATH}")
    endif()

    list(GET ALL_DIRS 0 DIR)
    set(${VAR} ${DIR} PARENT_SCOPE)
endfunction()

function(fetch_repo HOST REPO REF)
    if(HOST MATCHES "^\\$\\{(.+)\\}$")
        # Dereference variable
        set(HOST ${${CMAKE_MATCH_1}})
    endif()

    if(HOST STREQUAL "GH")
        set(HOST "https://github.com")
    elseif(TYPE STREQUAL "GL")
        set(HOST "https://gitlab.com")
    endif()

    set(URL "${HOST}/${REPO}.git")

    string(REPLACE "/" "_" REPO_DIR ${REPO})

    get_filename_component(DIR "${CMADE_CACHE}/${REPO_DIR}" REALPATH)

    if(CMADE_NO_CACHE)
        file(REMOVE_RECURSE "${DIR}")
    endif()

    set(GIT_ARGS "clone")
    list(
        APPEND GIT_ARGS
        -c advice.detachedHead=false
        --depth 1
    )

    if(REF)
        list(APPEND GIT_ARGS --branch "${REF}")
    endif()

    if(NOT IS_DIRECTORY "${DIR}/sources")
        info("cloning ${URL} in ${DIR}/sources")
        run_prog(CMD git ${GIT_ARGS} ${URL} "${DIR}/sources")
    endif()

    setg(CMADE_SOURCES "${DIR}/sources")
    setg(CMADE_BUILD "${DIR}/build")
endfunction()

function(fetch_url URL)
    string(MD5 HASH ${URL})

    if(URL MATCHES "/([^/]+)$")
        set(FILE ${CMAKE_MATCH_1})
    else()
        die("can't find filename from URL: ${URL}")
    endif()

    get_filename_component(DIR "${CMADE_CACHE}/${HASH}" REALPATH)

    if(CMADE_NO_CACHE)
        file(REMOVE_RECURSE "${DIR}")
    endif()

    if(NOT EXISTS "${DIR}/${FILE}")
        info("downloading ${URL} in ${DIR}")
        download(${URL} "${DIR}/${FILE}")
    endif()

    if(NOT IS_DIRECTORY "${DIR}/sources")
        info("extracting ${FILE}")
        file(
            ARCHIVE_EXTRACT
            INPUT "${DIR}/${FILE}"
            DESTINATION "${DIR}/sources"
        )
    endif()

    unique_dir("${DIR}/sources" SDIR)
    setg(CMADE_SOURCES ${SDIR})
    setg(CMADE_BUILD_DIR "${DIR}/build")
endfunction()

##############################################################################
#
# Dependency installation functions
#
##############################################################################
function(build_cmake_dep SRCDIR ARGS)
    run_prog(
        CMD
            ${CMAKE_COMMAND}
            -DCMAKE_PREFIX_PATH=${CMADE_ENV}
            -DCMAKE_INSTALL_PREFIX=${CMADE_ENV}
            -DCMAKE_BUILD_TYPE:STRING=Release
            -G Ninja -S ${SRCDIR} -B ${CMADE_BUILD} ${ARGS}
    )
    run_prog(CMD ${CMAKE_COMMAND} --build ${CMADE_BUILD} --parallel)
    run_prog(CMD ${CMAKE_COMMAND} --install ${CMADE_BUILD})
endfunction()

function(build_meson_dep SRCDIR ARGS)
    file(MAKE_DIRECTORY ${CMADE_BUILD})
    run_prog(
        DIR ${CMADE_BUILD}
        CMD
            meson
            --prefix=${CMADE_ENV}
            --pkg-config-path=${CMADE_ENV}
            --cmake-prefix-path=${CMADE_ENV}
            ${ARGS}
            . ${SRCDIR}
    )
    run_prog(meson install)
endfunction()

function(build_autotools_dep SRCDIR ARGS)
    file(MAKE_DIRECTORY ${CMADE_BUILD})
    run_prog(
        DIR ${CMADE_BUILD}
        CMD
            ${SRCDIR}/configure
            --prefix=${CMADE_ENV}
            ${ARGS}
    )
    run_prog(DIR ${CMADE_BUILD} make install)
endfunction()

function(build_dep ARGS)
    set(SRCDIR ${CMADE_SOURCES})
    string(REGEX MATCHALL "[^ \"']+|\"([^\"]*)\"|'([^']*)'" ARGS "${ARGS}")

    foreach(ARG ${ARGS})
        if(ARG MATCHES "^--srcdir=(.+)")
            set(SRCDIR "${SRCDIR}/${CMAKE_MATCH_1}")
        else()
            list(APPEND CONF_ARGS ${ARG})
        endif()
    endforeach()

    if(NOT IS_DIRECTORY "${SRCDIR}")
        die("invalid source directory: ${SRCDIR}")
    endif()

    if(EXISTS "${SRCDIR}/CMakeLists.txt")
        build_cmake_dep("${SRCDIR}" ${CONF_ARGS})
    elseif(EXISTS "${SRCDIR}/meson.build")
        build_meson_dep("${SRCDIR}" ${CONF_ARGS})
    elseif(EXISTS "${SRCDIR}/configure")
        build_autotools_dep("${SRCDIR}" ${CONF_ARGS})
    else()
        die("don't know how to build in ${SRCDIR}")
    endif()
endfunction()

function(install_repo HOST REPO TAG ARGS)
    fetch_repo(${HOST} ${REPO} "${TAG}")
    build_dep("${ARGS}")
endfunction()

function(install_url URL ARGS)
    msg("URL='${URL}' ARGS='${ARGS}'")
    fetch_url(${URL})
    build_dep("${ARGS}")
endfunction()

function(install_deps)
    if(NOT EXISTS ${CMDEPS})
        msg("no dependencies")
        return()
    endif()

    file(STRINGS ${CMDEPS} DEPS)

    foreach(SPEC ${DEPS})
        if(SPEC MATCHES "^#")
            # Skip comments
            continue()
        elseif(SPEC MATCHES "^([A-Za-z0-9_-]+)=(.+)$")
            # Variable assignment
            setg(${CMAKE_MATCH_1} "${CMAKE_MATCH_2}")
        elseif(SPEC MATCHES "^git\\+([^:]+):([^ @]+)(@([^ ]+))?( (.*))?$")
            set(HOST ${CMAKE_MATCH_1})
            set(REPO ${CMAKE_MATCH_2})
            set(TAG ${CMAKE_MATCH_4})
            set(ARGS "${CMAKE_MATCH_6}")
            install_repo(${HOST} ${REPO} "${TAG}" "${ARGS}")
        elseif(SPEC MATCHES "^([^ ]+)([ ](.+))?$")
            set(URL ${CMAKE_MATCH_1})
            set(ARGS "${CMAKE_MATCH_3}")
            install_url(${URL} "${ARGS}")
        else()
            die("invalid dependency line: ${SPEC}")
        endif()
    endforeach()
endfunction()

##############################################################################
#
# Argument functions
#
##############################################################################
function(locate_cmade_arguments)
    set(FOUND OFF)

    foreach(POS RANGE ${CMAKE_ARGC})
        string(TOLOWER "${CMAKE_ARGV${POS}}" ARG)
        math(EXPR POS "${POS}+1")

        if (ARG MATCHES "${CMADE}$")
            # Script args follow us, POS already incremented
            set(FOUND ON)
            setg(CMADE_POS ${POS})
            break()
        endif()
    endforeach()

    if(NOT FOUND)
        # Should not happen if script has correct name (see CMADE at top)
        die("parse_argument")
    endif()
endfunction()

function(parse_arguments)
    locate_cmade_arguments()

    while(CMADE_POS LESS ${CMAKE_ARGC})
        if ("${CMAKE_ARGV${CMADE_POS}}" MATCHES "--?([A-Za-z0-9_-]+)")
            list(FIND CMADE_OPTIONS ${CMAKE_MATCH_1} OPT)

            if (OPT LESS 0)
                die("unknown option: ${CMAKE_MATCH_1}")
            else()
                string(TOUPPER "CMADE_${CMAKE_MATCH_1}" OPT)
                string(REPLACE "-" "_" OPT "${OPT}")
                setg(${OPT} 1)
            endif()
        else()
            list(APPEND CMADE_ARGS "${CMAKE_ARGV${CMADE_POS}}")
        endif()

        math(EXPR CMADE_POS "${CMADE_POS}+1")
    endwhile()

    list(LENGTH CMADE_ARGS CMADE_ARGC)

    if (CMADE_ARGC GREATER 0)
        list(POP_FRONT CMADE_ARGS CMADE_CMD)
        setg(CMADE_CMD "${CMADE_CMD}")
    endif()

    setg(CMADE_ARGS "${CMADE_ARGS}")
    get_filename_component(CMADE_ENV "${CMADE_ENV}" REALPATH)
    setg(CMADE_ENV ${CMADE_ENV})
endfunction()

##############################################################################
#
# Command processing
#
##############################################################################
function(process_cmd)
    if (CMADE_CMD STREQUAL "install")
        install_deps()
    elseif (CMADE_CMD STREQUAL "help")
        show_help()
    elseif(CMADE_CMD)
        msg("unknown command: ${CMADE_CMD}")
    elseif(NOT CMADE_CMD)
        msg("no command")
    endif()
endfunction()

##############################################################################
#
# Main part
#
##############################################################################
parse_arguments()
process_cmd()
